:alias player-move          vd
:alias is-player-move-valid vc
:alias blank-index          vb

:const UP    0
:const LEFT  2
:const DOWN  1
:const RIGHT 3

:const K-UP    5
:const K-LEFT  7
:const K-DOWN  8
:const K-RIGHT 9

: display-board
	clear
	v1 := 2 # x position
	v2 := 2 # y position
	v3 := 0  # board index

	loop
		i := board
		i += v3
		load v0
		i := hex v0
		sprite v1 v2 5
		v3 += 1
		v1 += 6
		if v1 > 22 begin
			v2 += 6
			v1 := 2
		end
	if v3 != 16 then again
	
	return

: get-player-move
	loop
		v0 := key
		if v0 == K-UP begin
			player-move := UP
			return
		end
		if v0 == K-LEFT begin
			player-move := LEFT
			return
		end
		if v0 == K-DOWN begin
			player-move := DOWN
			return
		end
		if v0 == K-RIGHT begin
			player-move := RIGHT
			return
		end
	again

: check-player-move
	is-player-move-valid := 1

	if player-move == UP begin
		if blank-index < 4 then is-player-move-valid := 0
	end
	if player-move == LEFT begin
		v0 := blank-index
		v1 := 0b11
		v0 &= v1
		if v0 == 0 then is-player-move-valid := 0
	end
	if player-move == DOWN begin
		if blank-index > 11 then is-player-move-valid := 0
	end
	if player-move == RIGHT begin
		v0 := blank-index
		v1 := 0b11
		v0 &= v1
		if v0 == 3 then is-player-move-valid := 0
	end

	return

: make-move
	if player-move == UP begin
		v1 := blank-index
		v2 := 4
		v1 -= v2
		i := board
		i += v1
		load v0
		i := board
		i += blank-index
		save v0
		v0 := 0
		i := board
		i += v1
		save v0
		blank-index -= v2
	end
	if player-move == LEFT begin
		v1 := blank-index
		v2 := 1
		v1 -= v2
		i := board
		i += v1
		load v0
		i := board
		i += blank-index
		save v0
		v0 := 0
		i := board
		i += v1
		save v0
		blank-index -= v2
	end
	if player-move == DOWN begin
		v1 := blank-index
		v2 := 4
		v1 += v2
		i := board
		i += v1
		load v0
		i := board
		i += blank-index
		save v0
		v0 := 0
		i := board
		i += v1
		save v0
		blank-index += v2
	end
	if player-move == RIGHT begin
		v1 := blank-index
		v2 := 1
		v1 += v2
		i := board
		i += v1
		load v0
		i := board
		i += blank-index
		save v0
		v0 := 0
		i := board
		i += v1
		save v0
		blank-index += v2
	end

	return

: main
	blank-index := 15
	display-board
	: game-loop
		get-player-move
		check-player-move
		if is-player-move-valid == 0 then jump game-loop
		make-move
		display-board
	jump game-loop

: board
	 1  2  3  4
	 5  6  7  8
	 9 10 11 12
	13 14 15  0
